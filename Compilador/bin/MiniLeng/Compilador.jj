/**
 * JavaCC template file created by SF JavaCC plugin 1.5.28+ wizard for JavaCC 1.5.0+
 */

options
{
  static = true;
  ignore_case = true;
}

PARSER_BEGIN(Compilador)
package MiniLeng;
import Util.Simbolo;
import Util.Simbolo.Tipo_simbolo;
import Util.Simbolo.Tipo_variable;
import Util.Simbolo.Clase_parametro;
import Util.Tabla_Simbolos;
import Util.TablaHash;
import Util.RegistroExp;
import Util.Etiqueta;
import Util.TipoOperador;
import Util.TipoOperador.Tipo_Operador_Aditivo;
import Util.TipoOperador.Tipo_Operador_Multiplicativo;
import Util.TipoOperador.Tipo_Operador_Relacional;
import Exceptions.ErrorLexico;
import Exceptions.ErrorSemantico;
import Exceptions.ErrorSintactico;
import Warnings.DesbordamientoEnteros;
import java.io.File;
import java.io.IOException;
import java.util.LinkedList;
import java.io.FileWriter;
import java.io.PrintWriter;

public class Compilador
{
  // IniCio del nivel de declaraciones anidadas
  public static int nivel = 0;

  static final int DIRECCION_INICIAL = 3;

  // Variable de direccion 
  public static long dir = DIRECCION_INICIAL;

  public static PrintWriter pw;

  public static Tabla_Simbolos tabla = new Tabla_Simbolos();

  public static Etiqueta etiq = new Etiqueta();

  public static ErrorLexico eL = new ErrorLexico();

  public static void iniciar_pila()
  {
    dir = DIRECCION_INICIAL;
  }

  public static void incrementar_pila()
  {
    dir++;
  }

  public static void main(String args []) throws ParseException, IOException
  {
    /* nombre del fichero */
    String nombreArchivo;
    String path = "C:\\Users\\Gord\\Desktop\\programas\\";
    if (args [0].equals("-v"))
    {
      System.out.println("Compilacion en modo verbose");
      nombreArchivo = args [1];
    }
    else
    {
      System.out.println("Compilacion sin modo verbose");
      nombreArchivo = args [0];
    }
    // Ruta del fochero de escritura
    File ficheroSalida = new File("C:\\Users\\Gord\\Desktop\\programas\\" + nombreArchivo + ".code");
    FileWriter ficheroCode = new FileWriter(ficheroSalida);
    pw = new PrintWriter(ficheroCode);
    /* Completar la ruta del path */
    path += nombreArchivo + ".ml";
    System.out.println("LEYENDO FICHERO " + nombreArchivo + "\n");
    try
    {
      /* Crear el parser con respecto al fichero */
      Compilador parser = new Compilador(new java.io.FileInputStream(path));
      // Inicializacion de la tabla de simbolos
      tabla.inicializar_tabla();
      int res = Compilador.programa();
      if (args [0].equals("-v"))
      {
        /* Mostrar total de tokens */
        TablaHash.mostrarTokensNormales();
        /* Mostrar total de identificadores */
        TablaHash.mostrarIdentificadores();
      }
      // Cierre del flujo de escritura
      ficheroCode.close();
      // Balance de errores 
      int erroresLexicos = ErrorLexico.getNumErroresLexicos();
      int erroresSintacticos = ErrorSintactico.getNumErroresSintacticos();
      int erroresSemanticos = ErrorSemantico.getNumErroresSemanticos();
      // Comprobar que el fichero esta libre de errores lexicos, sintacticos o semanticos
      if (erroresLexicos > 0 || erroresSintacticos > 0 || erroresSemanticos > 0)
      {
        System.out.println("Compilacion finalizada con errores");
        System.out.println("==================================");
        System.out.println("Detectados " + erroresLexicos + " errores lexicos");
        System.out.println("Detectados " + erroresSintacticos + " errores sintacticos");
        System.out.println("Detectados " + erroresSemanticos + " errores semanticos");
        // Borrado del fichero .code creado
        ficheroSalida.delete();
      }
      else
      {
        System.out.println("Compilacion finalizada sin errores");
        System.out.println("==================================");
        System.out.println("El fichero " + nombreArchivo + " es correcto");
      }
      // Comprobar que la ejecucion ha ido correctamente
    }
    catch (Exception e)
    {
      System.out.println("INCORRECTO");
      System.out.println(e.getMessage());
      Compilador.ReInit(System.in);
    }
    catch (Error e)
    {
      // Obtencion del error sintactico 
      int fila = CompiladorTokenManager.input_stream.getBeginLine();
      int columna = CompiladorTokenManager.input_stream.getBeginColumn();
      String tokenMalo = CompiladorTokenManager.input_stream.GetImage();
      ErrorLexico.deteccionErrorLexico(fila, columna, tokenMalo);
    }
  }
}

PARSER_END(Compilador)

SKIP : /* CARACTERES A IGNORAR */
{
  " "
| "\r"
| "\t"
| "\n"
| "%%" : ComentLinea
}

< ComentLinea >
SKIP :
{
  "%%" : DEFAULT
}

< ComentLinea >
MORE :
{
  < ~[ ] >
}

SPECIAL_TOKEN : /* Comentario de una linea */
{
  < SINGLE_LINE_COMMENT :
    "%" (~[ "\n", "\r" ])*
    (
      "\n"
    | "\r"
    | "\r\n"
    ) >
}

TOKEN : /* OPERADORES ARITMETICOS */
{
  < tPLUS : "+" >
  {
    TablaHash.anyadirHashTokens("tPLUS");
  }
| < tMINUS : "-" >
  {
    TablaHash.anyadirHashTokens("tMINUS");
  }
| < tMULTIPLY : "*" >
  {
    TablaHash.anyadirHashTokens("tMULTIPLY");
  }
| < tDIVIDE : "/" >
  {
    TablaHash.anyadirHashTokens("tDIVIDE");
  }
| < tMAYOR : ">" >
  {
    TablaHash.anyadirHashTokens("tMAYOR");
  }
| < tMENOR : "<" >
  {
    TablaHash.anyadirHashTokens("tMENOR");
  }
| < tIGUAL : "=" >
  {
    TablaHash.anyadirHashTokens("tIGUAL");
  }
| < tMAI : ">=" >
  {
    TablaHash.anyadirHashTokens("tMAI");
  }
| < tMEI : "<=" >
  {
    TablaHash.anyadirHashTokens("tMEI");
  }
| < tNI : "<>" >
  {
    TablaHash.anyadirHashTokens("tNI");
  }
| < tOPAS : ":=" >
  {
    TablaHash.anyadirHashTokens("tOPAS");
  }
}

TOKEN : /* OPERADORES LOGICOS */
{
  < tAND : "and" >
  {
    TablaHash.anyadirHashTokens("tAND");
  }
| < tOR : "or" >
  {
    TablaHash.anyadirHashTokens("tOR");
  }
| < tNOT : "not" >
  {
    TablaHash.anyadirHashTokens("tNOT");
  }
}

TOKEN : /* PALABRAS RESERVADAS */
{
  < tPROGRAMA : "programa" >
  {
    TablaHash.anyadirHashTokens("tPROGRAMA");
  }
| < tVAR : "var" >
  {
    TablaHash.anyadirHashTokens("tVAR");
  }
| < tPRINCIPIO : "principio" >
  {
    TablaHash.anyadirHashTokens("tPRINCIPIO");
  }
| < tFIN : "fin" >
  {
    TablaHash.anyadirHashTokens("tFIN");
  }
| < tSI : "si" >
  {
    TablaHash.anyadirHashTokens("tSI");
  }
| < tENT : "ent" >
  {
    TablaHash.anyadirHashTokens("tENT");
  }
| < tSI_NO : "si_no" >
  {
    TablaHash.anyadirHashTokens("tSI_NO");
  }
| < tFSI : "fsi" >
  {
    TablaHash.anyadirHashTokens("tFSI");
  }
| < tMQ : "mq" >
  {
    TablaHash.anyadirHashTokens("tMQ");
  }
| < tFMQ : "fmq" >
  {
    TablaHash.anyadirHashTokens("tFMQ");
  }
| < tESCRIBIR : "escribir" >
  {
    TablaHash.anyadirHashTokens("tESCRIBIR");
  }
| < tLEER : "leer" >
  {
    TablaHash.anyadirHashTokens("tLEER");
  }
| < tMOD : "mod" >
  {
    TablaHash.anyadirHashTokens("tMOD");
  }
| < tDIV : "div" >
  {
    TablaHash.anyadirHashTokens("tDIV");
  }
| < tENTERO : "entero" >
  {
    TablaHash.anyadirHashTokens("tENTERO");
  }
| < tBOOLEANO : "booleano" >
  {
    TablaHash.anyadirHashTokens("tBOOLEANO");
  }
| < tCARACTER : "caracter" >
  {
    TablaHash.anyadirHashTokens("tCARACTER");
  }
| < tTRUE : "true" >
  {
    TablaHash.anyadirHashTokens("tTRUE");
  }
| < tFALSE : "false" >
  {
    TablaHash.anyadirHashTokens("tFALSE");
  }
| < tENTACAR : "entacar" >
  {
    TablaHash.anyadirHashTokens("tENTACAR");
  }
| < tCARAENT : "caraent" >
  {
    TablaHash.anyadirHashTokens("tCARAENT");
  }
| < tACCION : "accion" >
  {
    TablaHash.anyadirHashTokens("tACCION");
  }
| < tVAL : "val" >
  {
    TablaHash.anyadirHashTokens("tVAL");
  }
| < tREF : "ref" >
  {
    TablaHash.anyadirHashTokens("tREF");
  }
}

TOKEN : /*EXPRESIONES */
{
  < #tDIGITO : [ "0"-"9" ] >	
| < #tLETRA : [ "a"-"z" ] >
| < tCONSTCHAR : ("\"") (~[ "\"" ]) ("\"") >
  {
    TablaHash.anyadirHashTokens("tCONSTCHAR");
  }
| < tCONSTCAD : ("\"") (~[ "\"" ])+ ("\"") >
  {
    TablaHash.anyadirHashTokens("tCONSTCAD");
  }
| < tCONSTANTE_NUMERICA : (< tDIGITO >)+ >
  {
    TablaHash.anyadirHashTokens("tCONSTANTE_NUMERICA");
  }
| < tIDENTIFICADOR :
    (
      < tLETRA >
    |
      (
        (
          < tLETRA >
        | "_"
        )
        (
          < tLETRA >
        | "_"
        | < tDIGITO >
        )*
        (
          < tLETRA >
        | < tDIGITO >
        )
      )
    ) >
  {
    TablaHash.anyadirHashTokens(image.toString());
    TablaHash.anyadirHashIdentificadores(image.toString());
  }
}

TOKEN : /* OPERADORES SEPARADDORES */
{
  < tPUNTYCOM : ";" >
  {
    TablaHash.anyadirHashTokens("tPUNTYCOM");
  }
| < tCOMA : "," >
  {
    TablaHash.anyadirHashTokens("tCOMA");
  }
| < tPARENTESIS_IZDA : "(" >
  {
    TablaHash.anyadirHashTokens("tPARENTESIS_IZDA");
  }
| < tPARENTESIS_DCHA : ")" >
  {
    TablaHash.anyadirHashTokens("tPARENTESIS_DCHA");
  }
}

/* Construccion del analizador sintactico */

// Regla de programa
int programa() :
{
  Token tSim, t;
  Simbolo s;
  Simbolo.Tipo_simbolo tp_Sim;
}
{
  try
  {
    // Lectura del token programa
    < tPROGRAMA > 
    // Guardado del nombre del programa
    t = < tIDENTIFICADOR >
    {
      // Insertar en la tabla de simbolos el token del programa
      // no se comprueba porque es el primero
      s = tabla.introducir_programa(t.image, dir);
      // Escritura del nombre del programa en el fichero
      pw.println("; Programa " + t.image.toUpperCase() + ".");
      // Etiqueta inicial del programa
      String etiquetaProg = etiq.nueva_etiqueta();
      pw.println("\t ENP  " + etiquetaProg);
    }
    // Lectura del resto de datos
    < tPUNTYCOM > 
    declaracion_variables() declaracion_acciones()
    {
      // Mostrar el comienzo del programa con la etiqueta inicial
      pw.println("; Comienzo del programa " + t.image.toUpperCase());
      pw.println(etiquetaProg + ":");
    }
    bloque_sentencias()
    {
      // Fin del fichero
      pw.println("; Fin del programa " + t.image.toUpperCase() + ".");
      pw.println("\t LVP");
    }
    < EOF >
    {
      return 0;
    }
  }
  catch (ParseException e)
  {
    ErrorSintactico.deteccionErrorSintactico(e);
    return 1;
  }
}

// Regla de bloque_sentencias OK
void bloque_sentencias() :
{
}
{
  try
  {
    < tPRINCIPIO > lista_sentencias() < tFIN >
    {
      // Ocultar los parametros del nivel actual
      tabla.ocultar_parametros(nivel);
    }
  }
  catch (ParseException e)
  {
    ErrorSintactico.deteccionErrorSintactico(e);
  }
}

// Regla de lista_sentencias OK EN DUDA
void lista_sentencias() :
{
}
{
  try
  {
    // El bloque de sentencias tiene mas de una 
    (sentencia()) +
  }
  catch (ParseException e)
  {
    ErrorSintactico.deteccionErrorSintactico(e);
  }
}

// Regla de sentencia OK
void sentencia() :
{
}
{
  try
  {
    leer() < tPUNTYCOM >
  | escribir() < tPUNTYCOM >
  | asig_invoc()
  | seleccion()
  | mientras_que()
  }
  catch (ParseException e)
  {
    ErrorSintactico.deteccionErrorSintactico(e);
  }
}

void asig_invoc() :
{
  // Declaracion de variables
  Token t;
}
{
  try
  {
    // Pasar el token capturado como identificador a la invocacion de la accion
    t = < tIDENTIFICADOR > (asignacion(t)
  | invocacion_accion(t))
  }
  catch (ParseException e)
  {
    ErrorSintactico.deteccionErrorSintactico(e);
  }
}

// Regla de asignacion OK
void asignacion(Token t) :
{
  // Declaracion de variables
  RegistroExp tpExp;
  Simbolo s;
  Simbolo.Tipo_variable tipo = null;
  boolean ok = true;
}
{
  try
  {
    < tOPAS >
    {
      // Se busca el simbolo en la tabla de simbolos
      s = tabla.buscar_simbolo(t.image);
      // el simbolo se ha encontrado bien
      if (s == null)
      {
        // Excepcion de simbolo no encontrado
        ErrorSemantico.deteccionErrorSemantico("linea " + token.beginLine + 
        ", columna " + token.beginColumn + "  - Variable desconocida " + t.image + 
        " en la parte izquierda de la asignacion");
        // Tipo de simbolo desconocido
        tipo = Simbolo.Tipo_variable.DESCONOCIDO;
        ok = false;
      }
      else if (s.es_Simbolo_Parametro())
      {
        if (s.es_Parametro_Valor())
        {
          // El parametro es por valor
          ErrorSemantico.deteccionErrorSemantico("linea " + token.beginLine + 
          ", columna " + token.beginColumn + "  - Prohibido asignar a " + s.getNombre() + 
          ", es un parametro pasado como valor");
          // Tipo de simbolo desconocido
          tipo = Simbolo.Tipo_variable.DESCONOCIDO;
          ok = false;
        }
        else
        {
          // El parametro a asignar es por referencia
          tipo = s.getVariable();
          pw.println("; Direccion del parametro por referencia " + s.getNombre().toUpperCase() + ".");
          pw.println("\t SRF   " + (nivel - s.getNivel()) + "  " + s.getDir());
          pw.println("\t DRF");
          pw.println("\t DRF");
        }
      }
      else if (s.es_Simbolo_Accion())
      {
        ErrorSemantico.deteccionErrorSemantico("linea " + token.beginLine + 
        ", columna " + token.beginColumn + "  - Prohibida la asignacion a la accion " + 
        s.getNombre());
        // Tipo de simbolo desconocido
        tipo = Simbolo.Tipo_variable.DESCONOCIDO;
        ok = false;
      }
      else
      {
        pw.println("; Direccion de la variable " + s.getNombre().toUpperCase() + ".");
        pw.println("\t SRF   " + (nivel - s.getNivel()) + "  " + s.getDir());
        tipo = s.getVariable();
      }
    }
    // Procesamiento de la expresion
    tpExp = expresion() < tPUNTYCOM >
    {
      if (ok && tpExp.getTipo() != tipo && tpExp.getTipo() != Simbolo.Tipo_variable.DESCONOCIDO)
      {
        ErrorSemantico.deteccionErrorSemantico("linea " + token.beginLine + 
        ", columna " + token.beginColumn + "  - Tipos incompatibles en la asignacion: " + 
        "no se puede convetir " + tpExp.getTipo().toString() + " a " + tipo.toString());
      }
      else
      {
        // Asignacion evaluada correctamente
        pw.println("; Asignacion.");
        pw.println("\t ASG");
      }
    }
  }
  catch (ParseException e)
  {
    ErrorSintactico.deteccionErrorSintactico(e);
  }
}

// Regla de lista_asignables OK
void lista_asignables() :
{
  //Declaracion de variables
  LinkedList < String > listaIdentificadores;
  // identificador a evaluar
  String idActual;
}
{
  try
  {
    // Retorno de la lista de identificadores
    listaIdentificadores = identificadores()
    {
      // Tamaño de la lista de identificadores
      int dimension = listaIdentificadores.size();
      // Simbolo a evaluar
      Simbolo s;
      // Recorrido de la lista 
      for (int i = 0; i < dimension; i++)
      {
        // obtencion del i-esimo identificador
        idActual = listaIdentificadores.get(i);
        // Busqueda del simbolo en la tabla de simbolos
        s = tabla.buscar_simbolo(idActual);
        if (s == null)
        {
          ErrorSemantico.deteccionErrorSemantico("linea " + token.beginLine + 
          ", columna " + token.beginColumn + "  - Identificador de asignacion " 
          + idActual + " desconocido");
        }
        else if (s.getTipo() == Simbolo.Tipo_simbolo.VARIABLE 
        || s.getVariable() == Simbolo.Tipo_variable.DESCONOCIDO)
        {
          // es variable o desconocido
          if (s.es_Variable_Booleano() || s.es_Variable_Cadena())
          {
            // Error semantico en la lista de asignables
            ErrorSemantico.deteccionErrorSemantico("linea " + token.beginLine + 
            ", columna " + token.beginColumn + "  - Tipo invalido de variable de lectura, se espera " + 
            "entero o caracter");
          }
          else
          {
            // Lectura de variables      
            pw.println("; Leer variable " + s.getNombre());
            // Mostrar datos de la variable o parametro
            pw.println("\t SRF   " + (nivel - s.getNivel()) + "  " + s.getDir());
            pw.println("\t RD    1");
          }
        }
        else if (s.es_Simbolo_Parametro())
        {
          if (s.es_Parametro_Valor())
          {
            // Error semantico en la lista de asignables
            ErrorSemantico.deteccionErrorSemantico("linea " + token.beginLine + 
            ", columna " + token.beginColumn + "  - Variable " + s.getNombre() + 
            " por valor en lectura");
          }
          else
          {
            // Lectura de parametro por referencia      
            pw.println("; Leer parametro por referencia " + s.getNombre());
            // Mostrar datos de la variable o parametro
            pw.println("\t SRF   " + (nivel - s.getNivel()) + "  " + s.getDir());
            pw.println("\t DRF");
            pw.println("\t DRF");
            pw.println("\t RD    1");
          }
        }
        else if (s.es_Simbolo_Accion())
        {
          // Error semantico en la lista de asignables
          ErrorSemantico.deteccionErrorSemantico("linea " + token.beginLine + 
          ", columna " + token.beginColumn + "  - Los argumentos de una funcion solo pueden ser" + 
          " parametros o variables, encontrado " + s.getNombre() + " de tipo " + s.getTipo().toString());
        }	
      }
    }
  }
  catch (ParseException e)
  {
    ErrorSintactico.deteccionErrorSintactico(e);
  }
}

// Regla de leer OK
void leer() :
{
}
{
  try
  {
    < tLEER >
    {
      pw.println("; Lectura");
    }
    < tPARENTESIS_IZDA > lista_asignables() < tPARENTESIS_DCHA >
  }
  catch (ParseException e)
  {
    ErrorSintactico.deteccionErrorSintactico(e);
  }
}

// Regla de escribir OK
void escribir() :
{
}
{
  try
  {
    < tESCRIBIR >
    {
      pw.println("; Escritura");
    }
    < tPARENTESIS_IZDA > lista_escribibles() < tPARENTESIS_DCHA >
  }
  catch (ParseException e)
  {
    ErrorSintactico.deteccionErrorSintactico(e);
  }
}

// Regla de lista_escribibles OK
void lista_escribibles() :
{
}
{
  try
  {
    escribible() (< tCOMA > escribible()) *
  }
  catch (ParseException e)
  {
    ErrorSintactico.deteccionErrorSintactico(e);
  }
}

// Regla de lista_escribibles OK
void escribible() :
{
  // Declaracion de variables
  Token t = null;
  Simbolo s;
  RegistroExp regExp = null;
  RegistroExp result = new RegistroExp();
  boolean constCad = false, entCad = false;
}
{
  try
  {
    (
    t = < tCONSTCHAR >
    {
      constCad = true;
    }
  | t = < tCONSTCAD >
    {
      constCad = true;
    }
  | t = < tIDENTIFICADOR >
  | < tENTACAR > < tPARENTESIS_IZDA > regExp = expresion() < tPARENTESIS_DCHA >
    {
      entCad = true;
    }
    )
    {
      if (constCad)
      {
        // La expresion es una cadena
        String cad = t.image.replace("\"", "'");
        pw.println("; cadena " + cad + ".");
        // Obtencion de la cadena y la longitud 
        String cadena = t.image;
        int tamanyo = cadena.length();
        // Escribir el codigo asccii de cada caracter de la cadena
        for (int i = 1; i < tamanyo - 1; i++)
        {
          pw.println("\t STC   " + (int) cadena.charAt(i));
          pw.println("\t WRT   0");
        }
        result.setTipo(Simbolo.Tipo_variable.CADENA);
      }
      else if (entCad)
      {
        if (regExp.getTipo() != Simbolo.Tipo_variable.ENTERO)
        {
          ErrorSemantico.deteccionErrorSemantico("linea " + token.beginLine + 
          ", columna " + token.beginColumn + "  - Se esperaba una expresion del tipo" + 
          " ENTERO en la funcion entacar. Se ha recibido una expresion de tipo " + 
          regExp.getTipo().toString());
        }
        else
        {
          pw.println("\t WRT   0");
          result.setTipo(Simbolo.Tipo_variable.ENTERO);
        }
      }
      else
      {
        // Lo busca bien el token en la tabla de simbolos
        s = tabla.buscar_simbolo(t.image);
        if (s == null)
        {
          ErrorSemantico.deteccionErrorSemantico("linea " + token.beginLine + 
          ", columna " + token.beginColumn + "  - Variable a escribir " + s.getNombre() 
          + " desconocida ");
        }
        else if (s.es_Variable_Desconocido())
        {
          ErrorSemantico.deteccionErrorSemantico("linea " + token.beginLine + 
          ", columna " + token.beginColumn + "  - Variable " + t.image + " no valida para escritura");
        }
        else
        {
          // Correcto el identificador
          pw.println("; Acceso a la variable " + s.getNombre().toUpperCase() + ".");
          pw.println("\t SRF   " + (nivel - s.getNivel()) + "  " + s.getDir());
          pw.println("\t DRF");
          // Si es parametro
          if (s.es_Simbolo_Parametro())
          {
            pw.println("\t DRF");
          }
          pw.println("\t WRT   1");
        }
      }
    }
  }
  catch (ParseException e)
  {
    ErrorSintactico.deteccionErrorSintactico(e);
  }
}

// Regla de invocacion accion OK
void invocacion_accion(Token t) :
{
  // Declaracion de variables
  Simbolo s = null;
  boolean args = false;
}
{
  try
  {
    {
      // Busqueda del simbolo en la tabla
      s = tabla.buscar_simbolo(t.image);
      if (s == null)
      {
        ErrorSemantico.deteccionErrorSemantico("linea " + token.beginLine + 
        ", columna " + token.beginColumn + "  - Identificador " + t.image + 
        " desconocido en llamada a accion");
      }
      // Busqueda con exito en la tabla de simbolos
      else if (!s.es_Simbolo_Accion())
      {
        // error al invocar la accion
        ErrorSemantico.deteccionErrorSemantico("linea " + token.beginLine + 
        ", columna " + token.beginColumn + "  - No se puede realizar una llamada" + 
        " a una accion sobre el parametro " + t.image);
      }
      else
      {
        pw.println("; Invocacion a " + s.getNombre().toUpperCase() + ".");
      }
    }
    // Continuacion de evaluacion de la expresion
    (argumentos(s)
    {
      args = true;
    }
    ) ? < tPUNTYCOM >
    {
      // Si la accion no existe
      if (s != null)
      {
        // Comprobar el numero de parametros en caso de que se llame sin ninguno
        int argc = s.getLista_parametros().size();
        if (!args && argc != 0)
        {
          // Error por falta de parametros
          ErrorSemantico.deteccionErrorSemantico("linea " + token.beginLine + 
          ", columna " + token.beginColumn + " -  Se esperaban " + argc + 
          " parametros al invocar a la accion " + s.getNombre());
        }
        else
        {
          // La invocacion es correcta
          pw.println("\t OSF   " + s.getDir() + "  " + (nivel - s.getNivel()) + " " + s.getEtiqueta());
        }
      }
    }
  }
  catch (ParseException e)
  {
    ErrorSintactico.deteccionErrorSintactico(e);
  }
}

//Regla de mientras que OK
void mientras_que() :
{
  // Declaracion de variables
  RegistroExp tpExp;
  String etiqMQ, etiqFIN = null;
}
{
  try
  {
    < tMQ >
    {
      // Mostrar etiqueta de inicio de bucle
      etiqMQ = etiq.nueva_etiqueta();
      pw.println(etiqMQ + ":");
      pw.println("; MQ.");
    }
    tpExp = expresion()
    {
      if ((tpExp.getTipo() != Simbolo.Tipo_variable.DESCONOCIDO) 
      && (tpExp.getTipo() != Simbolo.Tipo_variable.BOOLEANO))
      {
        // Error en la condicion del mientras que
        ErrorSemantico.deteccionErrorSemantico("linea " + token.beginLine + 
        ", columna " + token.beginColumn + " - La condicion de mientras_que debe ser booleana");
      }
      else
      {
        // La condicion del bucle es correcta
        etiqFIN = etiq.nueva_etiqueta();
        pw.println("; Salir del bucle si la guarda se evalua a falso.");
        pw.println("\t JMF  " + etiqFIN);
      }
    }
    lista_sentencias() 
    < tFMQ >
    {
      // Fin de la iteracion del bucle
      pw.println("; Fin de la iteracion. Saltar a la cabecera del bucle.");
      pw.println("\t JMP  " + etiqMQ);
      pw.println(etiqFIN + ":");
      pw.println("; Fin MQ");
    }
  }
  catch (ParseException e)
  {
    ErrorSintactico.deteccionErrorSintactico(e);
  }
}

// Regla para los argumentos OK
void argumentos(Simbolo s) :
{
  //Declaracion de variables
  boolean ok = false;
}
{
  try
  {
    < tPARENTESIS_IZDA > (lista_expresiones(s)
    {
      // Captura bien la expresion
      ok = true;
    }
    ) ? < tPARENTESIS_DCHA >
    {
      // Comprobar los parametros de la invocacion en caso de que
      // se invoque con parametros distintos
      if (s != null)
      {
        // Si la accion existe
        int argc = s.getLista_parametros().size();
        if (!ok && argc != 0)
        {
          // Error de invocacion de parametros
          ErrorSemantico.deteccionErrorSemantico("linea " + token.beginLine + 
          " - Se esperaban " + argc + " parametros al invocar a la accion " + 
          s.getNombre());
        }
      }
    }
  }
  catch (ParseException e)
  {
    ErrorSintactico.deteccionErrorSintactico(e);
  }
}

// Regla de lista de expresiones
void lista_expresiones(Simbolo s) :
{
  // Declaracion de variables
  RegistroExp r;
  int argc = 0;
  boolean ok = true;
  LinkedList < Simbolo > parametros;
}
{
  try
  {
    // Captura de la primera expresion
    r = expresion()
    {
      if (s != null && !s.es_Variable_Desconocido())
      {
        // Obtencion de la lista de parametros de la accion
        parametros = s.getLista_parametros();
        argc++;
        // Numero de parametros incorrecto
        if (argc > parametros.size())
        {
          ErrorSemantico.deteccionErrorSemantico("linea " + token.beginLine + 
          ", columna " + token.beginColumn + "  - El numero de parametros de" + 
          " llamada a la funcion " + s.getNombre() + 
          " no coindice, se esperaban " + parametros.size());
          ok = false;
        }
        else if (r.getTipo() != parametros.get(argc - 1).getVariable())
        {
          ErrorSemantico.deteccionErrorSemantico("linea " + token.beginLine + 
          ", columna " + token.beginColumn + "  - Los tipos en la llamada a la funcion " + 
          s.getNombre() + " no coindicen");
          ok = false;
        }
        else if (r.getClase() == Simbolo.Clase_parametro.VAL && !parametros.get(argc - 1).es_Parametro_Valor())
        {
          ErrorSemantico.deteccionErrorSemantico("linea " + token.beginLine + 
          ", columna " + token.beginColumn + "  - Error al pasar un parametro por valor como referencia");
          ok = false;
        }
        else if (parametros.get(argc - 1).es_Parametro_Referencia() && r.isExpr_compuesta())
        {
          ErrorSemantico.deteccionErrorSemantico("linea " + token.beginLine + 
          ", columna " + token.beginColumn + "  - No se puede pasar al parametro por referencia " + 
          parametros.get(argc - 1).getNombre() + " una expresion compuesta");
          ok = false;
        }
        else
        {
          // Todo ha ido bien
          ok = false;
        }	
      }
    }
    (
    < tCOMA > 
    // Procesamiento de la nueva expresion
    r = expresion()
    {
      if (s != null && !s.es_Variable_Desconocido())
      {
        // Obtencion de la lista de parametros de la accion
        parametros = s.getLista_parametros();
        argc++;
        // Numero de parametros incorrecto
        if (argc > parametros.size())
        {
          ErrorSemantico.deteccionErrorSemantico("linea " + token.beginLine + 
          ", columna " + token.beginColumn + "  - El numero de parametros de" + 
          " llamada a la funcion " + s.getNombre() + 
          " no coindice, se esperaban " + parametros.size());
          ok = false;
        }
        // Comprobacion de los tipos en la funcion AQUI ESTAMOS
        else if (r.getTipo() != parametros.get(argc - 1).getVariable())
        {
          ErrorSemantico.deteccionErrorSemantico("linea " + token.beginLine + 
          ", columna " + token.beginColumn + "  - Los tipos en la llamada a la funcion" + 
          s.getNombre() + " no coindicen");
          ok = false;
        }
        else if (r.getClase() == Simbolo.Clase_parametro.VAL && !parametros.get(argc - 1).es_Parametro_Valor())
        {
          ErrorSemantico.deteccionErrorSemantico("linea " + token.beginLine + 
          ", columna " + token.beginColumn + "  - Error al pasar un parametro por valor como referencia");
          ok = false;
        }
        else if (parametros.get(argc - 1).es_Parametro_Referencia() && r.isExpr_compuesta())
        {
          ErrorSemantico.deteccionErrorSemantico("linea " + token.beginLine + 
          ", columna " + token.beginColumn + "  - No se puede pasar al parametro por referencia " + 
          parametros.get(argc - 1).getNombre() + " una expresion compuesta");
          ok = false;
        }
        else
        {
          // Todo ha ido bien
          ok = false;
        }
      }
    }
    ) *
  }
  catch (ParseException e)
  {
    ErrorSintactico.deteccionErrorSintactico(e);
  }
}

// Regla de expresion OK
RegistroExp expresion() :
{
  // Declaracion de expresiones a analizar
  RegistroExp tpExp1 = null, tpExp2 = null;
  TipoOperador op;
  boolean ok = true;
  RegistroExp regResult = new RegistroExp();
  boolean constantes = false;
  // Comparador de cadenas 	
}
{
  try
  {
    // Obtencion de la primera expresion
    tpExp1 = expresion_simple() 
    (op = operador_relacional() 
    // Obtencion de la segunda expresion
    tpExp2 = expresion_simple()
    {
      // Evaluacion de la expresion
      // Resultado de evaluar la expresion
      regResult = new RegistroExp();
      // Evaluar primer termino de la expresion
      if (tpExp1.getTipo() == Simbolo.Tipo_variable.DESCONOCIDO)
      {
        ErrorSemantico.deteccionErrorSemantico("linea " + token.beginLine + 
        ", columna " + token.beginColumn + "  - El operador 1 debe ser entero " + 
        "caracter, cadena o booleano");
        ok = false;
      }
      // Evaluar segundo termino de la expresion
      if (tpExp2.getTipo() == Simbolo.Tipo_variable.DESCONOCIDO)
      {
        ErrorSemantico.deteccionErrorSemantico("linea " + token.beginLine + 
        ", columna " + token.beginColumn + "  - El operador 2 debe ser entero " + 
        "caracter, cadena o booleano");
        ok = false;
      }
      // Comprobar que todo ha ido bien y evaluar la expresion
      if (ok)
      {
        // Verficar que los dos operandos son del mismo tipo
        if (tpExp1.getTipo() != tpExp2.getTipo())
        {
          ErrorSemantico.deteccionErrorSemantico("linea " + token.beginLine + 
          ", columna " + token.beginColumn + "  - Los operadores deben ser del mismo tipo " + 
          " en la expresion " + tpExp1.getTipo().toString() + " : " + tpExp2.getTipo().toString());
          regResult.setTipo(Simbolo.Tipo_variable.DESCONOCIDO);
        }
        else
        {
          // Tipo de la nueva expresion
          regResult.setTipo(Simbolo.Tipo_variable.BOOLEANO);
          // Evaluacion del tipo de la expresion
          switch (tpExp1.getTipo())
          {
            // La expresion puede ser entera
            case ENTERO : 
            // Comprobar que la segunda expresion es tambien entera
            ok = tpExp2.getTipo() == Simbolo.Tipo_variable.ENTERO;
            // Tipo de la nueva expresion
            regResult.setTipo(Simbolo.Tipo_variable.BOOLEANO);
            if (!ok)
            {
              // Resul tiene tipo DESCONOCIDO porque no coinciden
              regResult.setTipo(Simbolo.Tipo_variable.DESCONOCIDO);
            }
            // Son constantes
            else if (tpExp1.getSimbolo() == Simbolo.Tipo_simbolo.CONST 
            && tpExp2.getSimbolo() == Simbolo.Tipo_simbolo.CONST)
            {
              constantes = true;
              regResult.setSimbolo(Simbolo.Tipo_simbolo.CONST);
              switch (op.getOperadorRelacional())
              {
                case MAYOR : 
                pw.println("\t GT");
                regResult.valorBool = tpExp1.valorEnt > tpExp2.valorEnt;
                break;
                case MAYOR_IGUAL : 
                pw.println("\t GTE");
                regResult.valorBool = tpExp1.valorEnt >= tpExp2.valorEnt;
                break;
                case IGUAL : 
                pw.println("\t EQ");
                regResult.valorBool = tpExp1.valorEnt == tpExp2.valorEnt;
                break;
                case NO_IGUAL : 
                pw.println("\t NEQ");
                regResult.valorBool = tpExp1.valorEnt != tpExp2.valorEnt;
                break;
                case MENOR_IGUAL : 
                pw.println("\t LTE");
                regResult.valorBool = tpExp1.valorEnt <= tpExp2.valorEnt;
                break;
                case MENOR : 
                pw.println("\t LT");
                regResult.valorBool = tpExp1.valorEnt < tpExp2.valorEnt;
                break;
                default : 
                break;
              }
            }
            // Los operadores no son constantes
            else
            {
              switch (op.getOperadorRelacional())
              {
                case MAYOR : 
                pw.println("\t GT");
                break;
                case MAYOR_IGUAL : 
                pw.println("\t GTE");
                break;
                case IGUAL : 
                pw.println("\t EQ");
                break;
                case NO_IGUAL : 
                pw.println("\t NEQ");
                break;
                case MENOR_IGUAL : 
                pw.println("\t LTE");
                break;
                case MENOR : 
                pw.println("\t LT");
                break;
                default : 
                break;
              }
            }
            break;
            case CHAR : 
            // Cmprobar que la segunda expresion es una cadena
            // El tipo es booleano
            regResult.setTipo(Simbolo.Tipo_variable.BOOLEANO);
            ok = tpExp2.getTipo() == Simbolo.Tipo_variable.CHAR;
            if (!ok)
            {
              regResult.setTipo(Simbolo.Tipo_variable.DESCONOCIDO);
            }
            // Los datos son constantes y se puede trabajar
            else if (tpExp1.getSimbolo() == Simbolo.Tipo_simbolo.CONST 
            && tpExp2.getSimbolo() == Simbolo.Tipo_simbolo.CONST)
            {
              constantes = true;
              switch (op.getOperadorRelacional())
              {
                // Para los caracteres estan definidos todos los caracteres
                // se pueden obtener los resultados cuando son constantes
                case IGUAL : 
                pw.println("\t EQ");
                regResult.valorBool = tpExp1.valorString.equals(tpExp2.valorString);
                break;
                case NO_IGUAL : 
                pw.println("\t NEQ");
                regResult.valorBool = !tpExp1.valorString.equals(tpExp2.valorString);
                case MAYOR_IGUAL : 
                pw.println("\t GTE");
                regResult.valorBool = tpExp1.valorString.charAt(0) >= tpExp2.valorString.charAt(0);
                break;
                case MAYOR : 
                pw.println("\t GT");
                regResult.valorBool = tpExp1.valorString.charAt(0) > tpExp2.valorString.charAt(0);
                case MENOR : 
                pw.println("\t LTE");
                regResult.valorBool = tpExp1.valorString.charAt(0) <= tpExp2.valorString.charAt(0);
                break;
                case MENOR_IGUAL : 
                pw.println("\t LT");
                regResult.valorBool = tpExp1.valorString.charAt(0) <= tpExp2.valorString.charAt(0);
                break;
                default : 
                ErrorSemantico.deteccionErrorSemantico("linea " + token.beginLine + 
                ", columna " + token.beginColumn + "  - No se puede" + 
                " utilizar el operador relacional " + op.getOperadorRelacional().toString() + 
                " sobre caracteres");
              }
            }
            else
            {
              // Los operandos no son constantes
              switch (op.getOperadorRelacional())
              {
                case IGUAL : 
                pw.println("\t EQ");	
                break;
                case NO_IGUAL : 
                pw.println("\t NEQ");
                break;
                case MAYOR : 
                pw.println("\t GT");	
                break;
                case MAYOR_IGUAL : 
                pw.println("\t GTE");
                break;
                case MENOR : 
                pw.println("\t LT");	
                break;
                case MENOR_IGUAL : 
                pw.println("\t LTE");
                break;
                default : 
                ErrorSemantico.deteccionErrorSemantico("linea " + token.beginLine + 
                ", columna " + token.beginColumn + "  - No se puede" + 
                " utilizar el operador relacional " + op.getOperadorRelacional().toString() + 
                " sobre caracteres");
              }
            }
            break;
            case BOOLEANO : 
            // Cmprobar que la segunda expresion es una cadena
            // El tipo es booleano
            regResult.setTipo(Simbolo.Tipo_variable.BOOLEANO);
            ok = tpExp2.getTipo() == Simbolo.Tipo_variable.BOOLEANO;
            if (!ok)
            {
              regResult.setTipo(Simbolo.Tipo_variable.DESCONOCIDO);
            }
            else if (tpExp1.getSimbolo() == Simbolo.Tipo_simbolo.CONST 
            && tpExp2.getSimbolo() == Simbolo.Tipo_simbolo.CONST)
            {
              constantes = true;
              // Son constantes
              switch (op.getOperadorRelacional())
              {
                case IGUAL : 
                regResult.valorBool = tpExp1.valorBool == tpExp2.valorBool;
                pw.println("\t EQ");
                break;
                case NO_IGUAL : 
                pw.println("\t NEQ");
                regResult.valorBool = tpExp1.valorBool != tpExp2.valorBool;
                break;
                default : 
                ErrorSemantico.deteccionErrorSemantico("linea " + token.beginLine + 
                ", columna " + token.beginColumn + "  - No se puede" + 
                " utilizar el operador " + op.getOperadorRelacional().toString() + 
                " sobre una booleano");
              }
            }
            // Los operandos no son constantes
            else
            {
              switch (op.getOperadorRelacional())
              {
                case IGUAL : 
                pw.println("\t EQ");
                break;
                case NO_IGUAL : 
                pw.println("\t NEQ");	
                break;
                default : 
                ErrorSemantico.deteccionErrorSemantico("linea " + token.beginLine + 
                ", columna " + token.beginColumn + "  - No se puede" + 
                " utilizar el operador relacional " + op.getOperadorRelacional().toString() + 
                " sobre caracteres");
              }
            }
            break;
            case DESCONOCIDO : 
            regResult.setTipo(Simbolo.Tipo_variable.DESCONOCIDO);
            break;
            default : 
            ErrorSemantico.deteccionErrorSemantico("linea " + token.beginLine + 
            ", columna " + token.beginColumn + "  - El operador relacional es" + 
            " desconocido");
          }
        }
        if (ok && !constantes)
        {
          // Es expresion compuesta
          regResult.setExpr_compuesta(true);
        }
      }
    }
    ) *
    {
      // Comprobar que la expresion es compuesta
      if (tpExp2 == null)
      {
        return tpExp1;
      }
      else
      {
        return regResult;
      }
    }
  }
  catch (ParseException e)
  {
    ErrorSintactico.deteccionErrorSintactico(e);
  }
}

// Regla de operador_relacional OK
TipoOperador operador_relacional() :
{
  // Declaracion de variables
  TipoOperador op = new TipoOperador();
}
{
  try
  {
    < tIGUAL >
    {
      // Es el operador =
      op.setOperadorRelacional(TipoOperador.Tipo_Operador_Relacional.IGUAL);
      return op;
    }
  | < tMENOR >
    {
      // Es el operador <
      op.setOperadorRelacional(TipoOperador.Tipo_Operador_Relacional.MENOR);
      return op;
    }
  | < tMEI >
    {
      // Es el operador <=
      op.setOperadorRelacional(TipoOperador.Tipo_Operador_Relacional.MENOR_IGUAL);
      return op;
    }
  | < tNI >
    {
      // Es el operador <>
      op.setOperadorRelacional(TipoOperador.Tipo_Operador_Relacional.NO_IGUAL);
      return op;
    }
  | < tMAI >
    {
      // Es el operador >=
      op.setOperadorRelacional(TipoOperador.Tipo_Operador_Relacional.MAYOR_IGUAL);
      return op;
    }
  | < tMAYOR >
    {
      // Es el operador >
      op.setOperadorRelacional(TipoOperador.Tipo_Operador_Relacional.MAYOR);
      return op;
    }
  }
  catch (ParseException e)
  {
    ErrorSintactico.deteccionErrorSintactico(e);
  }
}

// Regla operador aditivo OK
TipoOperador operador_aditivo() :
{
  // Declaracion de variables
  TipoOperador op = new TipoOperador();
}
{
  try
  {
    < tPLUS >
    {
      // El operador es una suma
      op.setOperadorAditivo(TipoOperador.Tipo_Operador_Aditivo.SUMA);
      return op;
    }
  | < tMINUS >
    {
      // El operaodr es una resta
      op.setOperadorAditivo(TipoOperador.Tipo_Operador_Aditivo.RESTA);
      return op;
    }
  | < tOR >
    {
      // El operador es un OR
      op.setOperadorAditivo(TipoOperador.Tipo_Operador_Aditivo.OR);
      return op;
    }
  }
  catch (ParseException e)
  {
    ErrorSintactico.deteccionErrorSintactico(e);
  }
}

// regla de expresion simple OK
RegistroExp expresion_simple() :
{
  // Declaracion de variables
  RegistroExp regTerm1 = null, regTerm2 = null, regResult = null;
  TipoOperador op;
  boolean ok;
  boolean constantes = false;
}
{
  try
  {
    // Primer termino de la expresion
    regTerm1 = termino() 
    (
    // Operador de la expresion
    op = operador_aditivo() 
    // Segundo termino de la expresion
    regTerm2 = termino()
    {
      // Resultado de evaluar la expresion
      regResult = new RegistroExp();
      ok = true;
      // Evaluar el simbolo introducido
      if (op.getOperadorAditivo() == TipoOperador.Tipo_Operador_Aditivo.OR)
      {
        // Comprobar que son los dos booleanos
        ok = regTerm1.getTipo() == Simbolo.Tipo_variable.BOOLEANO 
        && regTerm2.getTipo() == Simbolo.Tipo_variable.BOOLEANO;
        if (!ok)
        {
          ErrorSemantico.deteccionErrorSemantico("linea " + token.beginLine + 
          ", columna " + token.beginColumn + "  - Incompatibilidad de expresiones en operacion: " + 
          regTerm1.getTipo().toString() + " y " + regTerm2.getTipo().toString());
          regResult.setTipo(Simbolo.Tipo_variable.DESCONOCIDO);
        }
        else
        {
          // Comprobar que son desconocidos
          if (regTerm1.getTipo() == Simbolo.Tipo_variable.DESCONOCIDO 
          || regTerm2.getTipo() == Simbolo.Tipo_variable.DESCONOCIDO)
          {
            regResult.setTipo(Simbolo.Tipo_variable.DESCONOCIDO);
          }
          else
          {
            // Son los dos booleanos
            regResult.setTipo(Simbolo.Tipo_variable.BOOLEANO);
            // Imprimir signo OR
            pw.println("\t OR");
            // Si son constantes calculamos la expresion
            if (regTerm1.getSimbolo() == Simbolo.Tipo_simbolo.CONST 
            && regTerm2.getSimbolo() != Simbolo.Tipo_simbolo.CONST)
            {
              constantes = true;
              regResult.setSimbolo(Simbolo.Tipo_simbolo.CONST);
              regResult.valorBool = regTerm1.valorBool
            | regTerm2.valorBool;
            }
          }
        }
      }
      // No es operador OR
      else
      {
        // Comprobar que son los dos enteros
        ok = regTerm1.getTipo() == Simbolo.Tipo_variable.ENTERO 
        && regTerm2.getTipo() == Simbolo.Tipo_variable.ENTERO;
        if (!ok)
        {
          ErrorSemantico.deteccionErrorSemantico("linea " + token.beginLine + 
          ", columna " + token.beginColumn + "  - Incompatibilidad de expresiones en operacion: " + 
          regTerm1.getTipo().toString() + " y " + regTerm2.getTipo().toString());
          regResult.setTipo(Simbolo.Tipo_variable.DESCONOCIDO);
        }
        // Comprobar que no son desconocidos
        else if (regTerm1.getTipo() == Simbolo.Tipo_variable.DESCONOCIDO 
        && regTerm2.getTipo() == Simbolo.Tipo_variable.DESCONOCIDO)
        {
          // El resultado es desconocido
          regResult.setTipo(Simbolo.Tipo_variable.DESCONOCIDO);
        }
        else
        {
          // La expresion final es entera          
          regResult.setTipo(Simbolo.Tipo_variable.ENTERO);
          if (DesbordamientoEnteros.hayDesbordamientoEntero(regTerm1.getValorEnt()) 
          || DesbordamientoEnteros.hayDesbordamientoEntero(regTerm2.getValorEnt()))
          {
            ErrorSemantico.deteccionErrorSemantico("linea " + token.beginLine + 
            ", columna " + token.beginColumn + "  - Valor fuera del rango");
            regResult.setTipo(Simbolo.Tipo_variable.DESCONOCIDO);
          }
          else
          {
            // Comprobar que son constantes
            if (regTerm1.getSimbolo() == Simbolo.Tipo_simbolo.CONST 
            && regTerm2.getSimbolo() == Simbolo.Tipo_simbolo.CONST)
            {
              constantes = true;
              regResult.setSimbolo(Simbolo.Tipo_simbolo.CONST);
              switch (op.getOperadorAditivo())
              {
                case SUMA : 
                pw.println("\t PLUS");
                regResult.valorEnt = regTerm1.valorEnt + regTerm2.valorEnt;
                break;
                case RESTA : 
                pw.println("\t SBT");
                regResult.valorEnt = regTerm1.valorEnt - regTerm2.valorEnt;
                break;
                default : 
                ErrorSemantico.deteccionErrorSemantico("linea " + token.beginLine + 
                ", columna " + token.beginColumn + "  - Operador aditivo desconocido");
              }
            }
            // Los operandos no son constantes
            else
            {
              switch (op.getOperadorAditivo())
              {
                case SUMA : 
                pw.println("\t PLUS");	
                break;
                case RESTA : 
                pw.println("\t SBT");	
                break;
                default : 
                ErrorSemantico.deteccionErrorSemantico("linea " + token.beginLine + 
                ", columna " + token.beginColumn + "  - Operador aditivo desconocido");
              }
            }
          }
        }
      }
      if (ok && !constantes)
      {
        regResult.setExpr_compuesta(true);
      }
    }
    ) *
    {
      // Comprobar que la expresion simple es compuesta
      if (regTerm2 == null)
      {
        return regTerm1;
      }
      else
      {
        return regResult;
      }
    }
  }
  catch (ParseException e)
  {
    ErrorSintactico.deteccionErrorSintactico(e);
  }
}

// LOS OPERADORES SON AND Y ESAS MIERDAS EH
// Regla de termino OK
RegistroExp termino() :
{
  // Declaracion de factores y expresiones
  RegistroExp tpFactor1 = null, tpFactor2 = null, regResult = null;
  // Declaracion del operador
  TipoOperador op;
  boolean ok;
  boolean constantes = false;
}
{
  try
  {
    // Evaluacion del primer factor
    tpFactor1 = factor() 
    (
    // Evaluacion del operador multiplicativo
    op = operador_multiplicativo() 
    // Evaluacion del segundo factor
    tpFactor2 = factor()
    {
      regResult = new RegistroExp();
      // El operaodr es una AND
      if (op.getOperadorMultiplicativo() == TipoOperador.Tipo_Operador_Multiplicativo.AND)
      {
        // Comprobar que son los dos booleanos
        ok = tpFactor1.getTipo() == Simbolo.Tipo_variable.BOOLEANO 
        && tpFactor2.getTipo() == Simbolo.Tipo_variable.BOOLEANO;
        if (!ok)
        {
          ErrorSemantico.deteccionErrorSemantico("linea " + token.beginLine + 
          ", columna " + token.beginColumn + "  - Incompatibilidad de expresiones en operacion: " + 
          tpFactor1.getTipo().toString() + " y " + tpFactor2.getTipo().toString());
          regResult.setTipo(Simbolo.Tipo_variable.DESCONOCIDO);
        }
        else if (tpFactor2.getTipo() == Simbolo.Tipo_variable.DESCONOCIDO 
        || tpFactor2.getTipo() == Simbolo.Tipo_variable.DESCONOCIDO)
        {
          // El resultado es desconocido
          regResult.setTipo(Simbolo.Tipo_variable.DESCONOCIDO);
        }
        else
        {
          regResult.setTipo(Simbolo.Tipo_variable.BOOLEANO);
          // Operador AND
          pw.println("\t AND");
          if (tpFactor1.getSimbolo() == Simbolo.Tipo_simbolo.CONST 
          && tpFactor2.getSimbolo() == Simbolo.Tipo_simbolo.CONST)
          {
            // Hacer operacion AND
            regResult.valorBool = tpFactor1.valorBool & tpFactor2.valorBool;
            regResult.setSimbolo(Simbolo.Tipo_simbolo.CONST);
          }
        }
      }
      else
      {
        // Es un operador multiplicativo distinto de AND
        ok = tpFactor1.getTipo() == Simbolo.Tipo_variable.ENTERO 
        && tpFactor2.getTipo() == Simbolo.Tipo_variable.ENTERO;
        if (!ok)
        {
          ErrorSemantico.deteccionErrorSemantico("linea " + token.beginLine + 
          ", columna " + token.beginColumn + "  - Incompatibilidad de expresiones en operacion: " + 
          tpFactor1.getTipo().toString() + " y " + tpFactor2.getTipo().toString());
          regResult.setTipo(Simbolo.Tipo_variable.DESCONOCIDO);
        }
        else
        {
          regResult.setTipo(Simbolo.Tipo_variable.ENTERO);
          if (DesbordamientoEnteros.hayDesbordamientoEntero(tpFactor1.valorEnt) 
          || DesbordamientoEnteros.hayDesbordamientoEntero(tpFactor2.valorEnt))
          {
            ErrorSemantico.deteccionErrorSemantico("linea " + token.beginLine + 
            ", columna " + token.beginColumn + "  - Valor fuera del rango");
            regResult.setTipo(Simbolo.Tipo_variable.DESCONOCIDO);
          }
          else
          {
            // Comprobar que son constantes para operar
            if (tpFactor1.getSimbolo() == Simbolo.Tipo_simbolo.CONST 
            && tpFactor2.getSimbolo() == Simbolo.Tipo_simbolo.CONST)
            {
              constantes = true;
              // El resultado tambien es constante
              regResult.setSimbolo(Simbolo.Tipo_simbolo.CONST);
              switch (op.getOperadorMultiplicativo())
              {
                case MULTIPLICACION : 
                pw.println("\t TMS");
                regResult.valorEnt = tpFactor1.valorEnt * tpFactor2.valorEnt;
                break;
                case DIVISION : 
                pw.println("\t DIV");
                if (tpFactor2.valorEnt == 0)
                {
                  ErrorSemantico.deteccionErrorSemantico("linea " + token.beginLine + 
                  ", columna " + token.beginColumn + "  - Division por 0");					
                  regResult.setTipo(Simbolo.Tipo_variable.DESCONOCIDO);				
                }
                else
                {
                  regResult.valorEnt = tpFactor1.valorEnt / tpFactor2.valorEnt;
                }
                break;
                case MOD : 
                pw.println("\t MOD");
                if (tpFactor2.valorEnt == 0)
                {
                  ErrorSemantico.deteccionErrorSemantico("linea " + token.beginLine + 
                  ", columna " + token.beginColumn + "  - Modulo por 0");					
                  regResult.setTipo(Simbolo.Tipo_variable.DESCONOCIDO);		
                }
                else
                {
                  regResult.valorEnt = tpFactor1.valorEnt % tpFactor2.valorEnt;
                }
                break;
                default : 
                ErrorSemantico.deteccionErrorSemantico("linea " + token.beginLine + 
                ", columna " + token.beginColumn + "  - Operador multiplicativo desconocido");					
              }
            }
            else
            {
              // Los operadores no son constantes
              switch (op.getOperadorMultiplicativo())
              {
                case MULTIPLICACION : 
                pw.println("\t TMS");
                break;
                case DIVISION : 
                pw.println("\t DIV");
                break;
                case MOD : 
                pw.println("\t MOD");
                break;
                default : 
                ErrorSemantico.deteccionErrorSemantico("linea " + token.beginLine + 
                ", columna " + token.beginColumn + "  - Operador multiplicativo desconocido");					
              }
            }
          }
        }
      }
      if (ok && !constantes)
      {
        // Es expresion compuesta
        regResult.setExpr_compuesta(true);
      }
    }
    ) *
    {
      // Comprobar que es expresion compuesta
      if (tpFactor2 == null)
      {
        return tpFactor1;
      }
      else
      {
        return regResult;
      }
    }
  }
  catch (ParseException e)
  {
    ErrorSintactico.deteccionErrorSintactico(e);
  }
}

// Regla de operador multiplicativo OK
TipoOperador operador_multiplicativo() :
{
  // Declaracion de variables
  TipoOperador op = new TipoOperador();
}
{
  try
  {
    < tMULTIPLY >
    {
      // El operador es una multiplicacion
      op.setOperadorMultiplicativo(TipoOperador.Tipo_Operador_Multiplicativo.MULTIPLICACION);
      return op;
    }
  | < tDIVIDE >
    {
      // El operador es un cociente (opcion 1)
      op.setOperadorMultiplicativo(TipoOperador.Tipo_Operador_Multiplicativo.DIVISION);
      return op;
    }
  | < tDIV >
    {
      // El operador es un cociente (opcion 2)
      op.setOperadorMultiplicativo(TipoOperador.Tipo_Operador_Multiplicativo.DIVISION);
      return op;
    }
  | < tMOD >
    {
      // El operador es un modulo o residuo
      op.setOperadorMultiplicativo(TipoOperador.Tipo_Operador_Multiplicativo.MOD);
      return op;
    }
  | < tAND >
    {
      // El operador es una AND logica
      op.setOperadorMultiplicativo(TipoOperador.Tipo_Operador_Multiplicativo.AND);
      return op;
    }
  }
  catch (ParseException e)
  {
    ErrorSintactico.deteccionErrorSintactico(e);
  }
}

// Regla de factor OK
RegistroExp factor() :
{
  // Declaracion de factores y expresiones
  RegistroExp tpFactor, tpExp;
  RegistroExp result = new RegistroExp();
  // Token a procesar
  Token t;
}
{
  try
  {
    < tNOT > 
    tpFactor = factor()
    {
      // Comprobacion de si es o no booleano
      if ((tpFactor.getTipo() != Simbolo.Tipo_variable.BOOLEANO) 
      && (tpFactor.getTipo() != Simbolo.Tipo_variable.DESCONOCIDO))
      {
        // Comprobacion de si es booleano o no 
        ErrorSemantico.deteccionErrorSemantico("linea " + token.beginLine + 
        ", columna " + token.beginColumn + "  - Tipo " + tpFactor.getTipo() + " incompatible con " + 
        " operador <>. Se esperaba BOOLEANO ");
        result.setTipo(Simbolo.Tipo_variable.DESCONOCIDO);
      }
      else
      {
        // Operador de negacion
        pw.println("\t NGB");
        result.setTipo(Simbolo.Tipo_variable.BOOLEANO);
      }
      // Devuelve el tipo de factor
      return result;
    }
  | < tMINUS > tpFactor = factor()
    {
      // Comprobacion de si es o no booleano
      if ((tpFactor.getTipo() != Simbolo.Tipo_variable.ENTERO) 
      && (tpFactor.getTipo() != Simbolo.Tipo_variable.DESCONOCIDO))
      {
        // Comprobacion de si es booleano o no 
        ErrorSemantico.deteccionErrorSemantico("linea " + token.beginLine + 
        ", columna " + token.beginColumn + "  - Tipo " + tpFactor.getTipo().toString() + 
        " incompatible con operador -. Se esperaba ENTERO");
        result.setTipo(Simbolo.Tipo_variable.DESCONOCIDO);
      }
      else
      {
        // Operador de negacion
        pw.println("\t NGBI");
        result.setTipo(Simbolo.Tipo_variable.ENTERO);
      }
      // Devuelve el tipo de factor
      return result;
    }
  | < tPARENTESIS_IZDA > tpExp = expresion() < tPARENTESIS_DCHA >
    {
      // Devuelve la expresiom normal
      return tpExp;
    }
  | t = < tENTACAR > < tPARENTESIS_IZDA > tpExp = expresion() < tPARENTESIS_DCHA >
    {
      // Comprobacion de si es entera la expresion
      if ((tpExp.getTipo() != Simbolo.Tipo_variable.ENTERO) 
      && (tpExp.getTipo() != Simbolo.Tipo_variable.DESCONOCIDO))
      {
        // Comprobacion de si es booleano o no 
        ErrorSemantico.deteccionErrorSemantico("linea " + token.beginLine + 
        ", columna " + token.beginColumn + "  - Argumento de tipo " + tpExp.getTipo().toString() + 
        " incompatible en funcion ENTACAR. Se esperaba ENTERO");
        result.setTipo(Simbolo.Tipo_variable.DESCONOCIDO);
      }
      else
      {
        // Es un entero y se comprueba que no hay desbordamiento
        int valor = tpExp.getValorEnt();
        // Error de desbordamiento
        if (ErrorSemantico.hayDesbordamientoEntacar(valor))
        {
          // Comprobacion de si es booleano o no 
          ErrorSemantico.deteccionErrorSemantico("linea " + token.beginLine + 
          ", columna " + token.beginColumn + "  - La operacion ENTACAR debe recibir un par\u00e1metro " + 
          "del tipo entero comprendido entre 0 y 255");
        }
        else
        {
          // Comprobar que es constante
          if (tpExp.getSimbolo() == Simbolo.Tipo_simbolo.CONST)
          {
            // Extraigo el numero entero y lo guardo a caracter
            result.setValorString(String.valueOf((char) valor));
            result.setTipo(Simbolo.Tipo_variable.CHAR);
          }
          else
          {
          // GENERADOR DE CODIGO
          }
          // Lo guardo en CHAR    
          result.setTipo(Simbolo.Tipo_variable.CHAR);
        }
      }
      return result;
    }
  | < tCARAENT > < tPARENTESIS_IZDA > tpExp = expresion() < tPARENTESIS_DCHA >
    {
      if ((tpExp.getTipo() != Simbolo.Tipo_variable.CHAR) 
      && (tpExp.getTipo() != Simbolo.Tipo_variable.DESCONOCIDO))
      {
        // Comprobacion de si es o no caracter
        ErrorSemantico.deteccionErrorSemantico("linea " + token.beginLine + 
        ", columna " + token.beginColumn + "  - Argumento de tipo " + tpExp.getTipo().toString() + 
        " incompatible en funcion CARAENT. Se esperaba CARACTER");
        result.setTipo(Simbolo.Tipo_variable.DESCONOCIDO);
      }
      else
      {
        if (tpExp.getSimbolo() == Simbolo.Tipo_simbolo.CONST)
        {
          // Obtener el tipo de dato entero del caracter porque es cosnstante
          char cadena = tpExp.valorString.charAt(0);
          result.setValorEnt((int) cadena);
        }
        else
        {
        // No es constante
        }
        result.setTipo(Simbolo.Tipo_variable.ENTERO);
      }
      return result;
    }
  | t = < tIDENTIFICADOR >
    {	
      Simbolo s;
      // Busqueda en la tabla de simbolos	
      s = tabla.buscar_simbolo(t.image);
      if (s == null)
      {
        ErrorSemantico.deteccionErrorSemantico("linea " + token.beginLine + 
        ", columna " + token.beginColumn + "  - Variable " + t.image + " no definida");
        tabla.introducir_variable(t.image, Simbolo.Tipo_variable.DESCONOCIDO, nivel, 0);
        result.setTipo(Simbolo.Tipo_variable.DESCONOCIDO);		
      }
      else if (s.es_Simbolo_Accion())
      {
        ErrorSemantico.deteccionErrorSemantico("linea " + token.beginLine + 
        ", columna " + token.beginColumn + "  - La accion " + s.getNombre() + " no puede " + 
        " formar parte de una expresion");
        tabla.introducir_variable(t.image, Simbolo.Tipo_variable.DESCONOCIDO, nivel, 0);
        result.setTipo(Simbolo.Tipo_variable.DESCONOCIDO);
      }
      else
      {
        result.setTipo(s.getVariable());
        result.setClase(s.getParametro());
        // El identificador es correcto
        pw.println("; Acceso a la variable " + s.getNombre().toUpperCase() + ".");
        pw.println("\t SRF   " + (nivel - s.getNivel()) + "  " + s.getDir());
        pw.println("\t DRF");
        if (s.es_Simbolo_Parametro())
        {
          pw.println("\t DRF");
        }
      }
      return result;	
    }
  | t = < tCONSTANTE_NUMERICA >
    {
      // Y si es entacar
      int valor = Integer.parseInt(t.image);
      result.setValorEnt(valor);
      // Tipo de la variable entero
      result.setTipo(Simbolo.Tipo_variable.ENTERO);
      // El simbolo es una constante
      result.setSimbolo(Simbolo.Tipo_simbolo.CONST);
      // Escritura de la constante en el fichero
      pw.println("\t STC   " + t.image);
      // Devolucion del resultado
      return result;
    }
  | t = < tCONSTCHAR >
    {
      if (t.image.length() > 3)
      {
        ErrorSemantico.deteccionErrorSemantico("linea " + token.beginLine + 
        ", columna " + token.beginColumn + "  - No se puede usar el caracter " + t.image + 
        " en una expresion");
      }
      else
      {
        // El simbolo es una constante
        result.setSimbolo(Simbolo.Tipo_simbolo.CONST);
        result.valorString = String.valueOf(t.image.charAt(1));
      }
      // Escribir constante en el fichero
      pw.println("\t STC   " + t.image);
      result.setTipo(Simbolo.Tipo_variable.CHAR);
      return result;
    }
  | t = < tCONSTCAD >
    {
      if (t.image.length() > 3)
      {
        ErrorSemantico.deteccionErrorSemantico("linea " + token.beginLine + 
        ", columna " + token.beginColumn + "  - No se puede usar la cadena " + t.image + 
        " en una expresion");		
      }
      else
      {
        // No coger las comillas
        // El simbolo es una constante
        result.setSimbolo(Simbolo.Tipo_simbolo.CONST);
        result.valorString = String.valueOf(t.image.charAt(1));
      }
      // Escribir constante en el fichero
      pw.println("\t STC   " + t.image);
      result.setTipo(Simbolo.Tipo_variable.CADENA);
      return result;
    }
  | < tTRUE >
    {
      // El simbolo es una constante
      result.setSimbolo(Simbolo.Tipo_simbolo.CONST);
      // Guardar el contenido de la cadena
      result.setValorBool(true);
      // Tipo cadena de caracteres
      result.setTipo(Simbolo.Tipo_variable.BOOLEANO);
      // Escribir constante en el fichero
      pw.println("\t STC   1");
      // Devolucion del resultado
      return result;
    }
  | < tFALSE >
    {
      // El simbolo es una constante
      result.setSimbolo(Simbolo.Tipo_simbolo.CONST);
      // Guardar el contenido de la cadena
      result.setValorBool(false);
      // Tipo cadena de caracteres
      result.setTipo(Simbolo.Tipo_variable.BOOLEANO);
      // Escribir constante en el fichero
      pw.println("\t STC   0");
      // Devolucion del resultado
      return result;
    }
  }
  catch (ParseException e)
  {
    ErrorSintactico.deteccionErrorSintactico(e);
  }
}

// Regla de seelccion OK
void seleccion() :
{
  // Declaracion de variables
  RegistroExp tpExp;
  String etiqSINO = null, etiqFIN;
}
{
  try
  {
    < tSI >
    {
      pw.println("; SI.");
    }
    tpExp = expresion()
    {
      // Evaluacion de la condicion
      if ((tpExp.getTipo() != Simbolo.Tipo_variable.DESCONOCIDO) 
      && (tpExp.getTipo() != Simbolo.Tipo_variable.BOOLEANO))
      {
        // Se esperaba una condicion booleana
        ErrorSemantico.deteccionErrorSemantico("linea " + token.beginLine + 
        ", columna " + token.beginColumn + "  - La condicion en la seleccion debe ser un booleano");
      }
      else
      {
        // La condicion es correcta
        etiqSINO = etiq.nueva_etiqueta();
        pw.println("\t JMF " + etiqSINO);
      }
    }
    < tENT >
    {
      pw.println("; ENT.");
    }
    lista_sentencias()
    {
      etiqFIN = etiq.nueva_etiqueta();
      pw.println("\t JMP " + etiqFIN);
      pw.println(etiqSINO + ":");
    }
    (
    < tSI_NO >
    {
      pw.println("; SI_NO.");
    }
    lista_sentencias()
    ) ? 
    < tFSI >
    {
      pw.println(etiqFIN + ":");
      pw.println("; Fin SI");
    }
  }
  catch (ParseException e)
  {
    ErrorSintactico.deteccionErrorSintactico(e);
  }
}

// Regla de declaracion_acciones OK
void declaracion_acciones() :
{
}
{
  try
  {
    (declaracion_accion()) *
  }
  catch (ParseException e)
  {
    ErrorSintactico.deteccionErrorSintactico(e);
  }
}

// Regla de declaracion_Accion OK
void declaracion_accion() :
{
  Token t;
}
{
  try
  {
    t = cabecera_accion() < tPUNTYCOM > declaracion_variables() declaracion_acciones() bloque_sentencias()
    {
      // Detectado el fin de un bloque de sentencias de una accion
      pw.println("; Fin de la accion / funcion " + t.image.toUpperCase() + ".");
      pw.println("\t CSF");
      // Eliminacion de variables
      tabla.eliminar_variables(nivel);
      // Eliminar las acciones
      tabla.eliminar_acciones(nivel);
      // Eliminar los parametros ocultos
      tabla.eliminar_parametros_ocultos(nivel);
      // Decrementar el nivel porque se cierra un bloque       
      nivel--;
    }
  }
  catch (ParseException e)
  {
    ErrorSintactico.deteccionErrorSintactico(e);
  }
}

// Regla de cabecera_accion OK
Token cabecera_accion() :
{
  Token tId = null;
  ;
  Simbolo s = null;
  Simbolo.Tipo_simbolo tp_Sim;
  boolean ok = false;
  // Lista de parametros de la accion
  LinkedList < LinkedList < Simbolo > > listaDeParametros = new LinkedList < LinkedList < Simbolo > > ();
  // Lista de simbolos
  LinkedList < Simbolo > listaAuxiliar = new LinkedList < Simbolo > ();
}
{
  try
  {
    // Apilar la nueva definicion de accion si no existe   
    < tACCION > 
    tId = < tIDENTIFICADOR >
    {
      // Buscar el simbolo en la tabla de simbolos
      s = tabla.buscar_simbolo(tId.image);
      if ((s == null) || (s.getNivel() != nivel))
      {
        // Introducir accion en la tabla de simbolos
        s = tabla.introducir_accion(tId.image, nivel, dir);
        ok = true;
        // Crear etiqueta para la accion
        String etiqAccion = etiq.nueva_etiqueta();
        s.setEtiqueta(etiqAccion);
        pw.println("; Accion " + tId.image.toUpperCase() + ".");
        pw.println(etiqAccion + ":");
        pw.println("; Comienzo de la accion " + tId.image.toUpperCase() + ".");
      }
      else
      {
        ErrorSemantico.deteccionErrorSemantico("linea " + token.beginLine + 
        ", columna " + token.beginColumn + "  - Accion " + s.getNombre() + " duplicada");
        ok = false;
      }
      // Incrementar el nivel actual
      nivel++;
      // Incio del marco de la pila
      iniciar_pila();
    }
    // Procesamiento de los parametros
    listaDeParametros = parametros_formales(tId)
    {
      // Limpiar parametros de la posible acc
      if (ok)
      {
        for (int i = 0; i < listaDeParametros.size(); i++)
        {
          // Añadir la lista de parametros al simbolo con la direccion correspondiente
          s.anyadirParametrosAccion(listaDeParametros.get(i), dir);
          // Preparar direccion de pila para el proximo parametro o variable
          incrementar_pila();
        }
        for (int i = listaDeParametros.size() - 1; i >= 0; i--)
        {
          listaAuxiliar = listaDeParametros.get(i);
          for (int j = listaAuxiliar.size() - 1; j >= 0; j--)
          {
            s = listaAuxiliar.get(j);
            // Generar codigo de los parametros
            pw.println("; rec. parametro " + s.getNombre().toUpperCase() + " de tipo " + s.getVariable().toString() + 
            " pasado por " + s.getParametro().toString());
            pw.println("\t SRF   " + (nivel - s.getNivel()) + "  " + s.getDir());
            pw.println("\t ASGI");
          }
        }
        // Limpiar parametros de la posible accion anterior
        tabla.limpiarListaParametros();
        // Fin del procesamiento de parametros
        String etiqPar = etiq.nueva_etiqueta();
        pw.println("\t JMP   " + etiqPar);
        pw.println(etiqPar + ":");
      }
      return tId;
    }
  }
  catch (ParseException e)
  {
    ErrorSintactico.deteccionErrorSintactico(e);
  }
}

// Regla de parametros formales OK
LinkedList < LinkedList < Simbolo > > parametros_formales(Token t) :
{
  // Lista global de listas de identificadores
  LinkedList < LinkedList < Simbolo > > parametros = new LinkedList < LinkedList < Simbolo > > ();
}
{
  try
  {
    (parametros = lista_parametros()
    {
      tabla.asignarListaParametros(parametros);
    }
    ) ?
    // Devolucion de todas las listas procesadas
    {
      return tabla.getListasParametros();
    }
  }
  catch (ParseException e)
  {
    ErrorSintactico.deteccionErrorSintactico(e);
  }
}

// Regla para lista_parametros OK
LinkedList < LinkedList < Simbolo > > lista_parametros() :
{
  // Lista global de listas de identificadores
  LinkedList < LinkedList < Simbolo > > listaGlobal = new LinkedList < LinkedList < Simbolo > > ();
  // Lista de identificadores
  LinkedList < Simbolo > lista = new LinkedList < Simbolo > ();
}
{
  try
  {
    < tPARENTESIS_IZDA > 
    (
    lista = parametros()
    {
      listaGlobal.add(lista);		
    }
    (< tPUNTYCOM > lista = parametros()
    {
      listaGlobal.add(lista);
    }
    ) * < tPARENTESIS_DCHA >
    {
      return listaGlobal;
    }
  | < tPARENTESIS_DCHA >
    {
      return listaGlobal;
    }
    )
  }
  catch (ParseException e)
  {
    ErrorSintactico.deteccionErrorSintactico(e);
  }
}

// Regla de parametros OK
LinkedList < Simbolo > parametros() :
{
  // Declaracion de variables
  // Simbolo a guardar en la tabla de simbolos
  Simbolo s;
  // Clase de parametro y tipo de variable del simbolo a introducir
  Simbolo.Clase_parametro cl_Param;
  Simbolo.Tipo_variable tipo_Var;
  // Lista de simbolos leidos a almacenar en la tabla de simbolos
  LinkedList < Simbolo > lista = new LinkedList < Simbolo > ();
  // Lista de identificadores procesados
  LinkedList < String > listaIdentificadores = new LinkedList < String > ();
}
{
  try
  {
    // Lectura del tipo de clase y del tipo de parametro
    cl_Param = clase_parametros() 
    tipo_Var = tipos_variables() 
    listaIdentificadores = identificadores()
    {
      // Tamaño de la lista de identificadores
      int dimension = listaIdentificadores.size();
      // Identificador del simbolo a procesar
      String identificadorActual;
      // Bucle de recorrido de la lista de identificadores
      for (int i = 0; i < dimension; i++)
      {
        // Obtener identificador actual
        identificadorActual = listaIdentificadores.get(i);
        // Comprobar que existe o no simbolo en la tabla
        // Insercion del parametro en la tabla de simbolos
        s = tabla.introducir_parametro(identificadorActual, tipo_Var, cl_Param, nivel, dir);
        // Si se ha creado el simbolo
        if (s != null)
        {
          // Añadir parametro a la lista de parametros
          lista.add(s);
        }
        else
        {
          // Comprobar que la variable esta en la tabla de simbolos
          ErrorSemantico.deteccionErrorSemantico("linea " + token.beginLine + 
          ", columna " + token.beginColumn + "  - Parametro repetido " + identificadorActual);
        }
      }
      return lista;
    }
  }
  catch (ParseException e)
  {
    ErrorSintactico.deteccionErrorSintactico(e);
  }
}

// Regla de declaracion variables OK
int declaracion_variables() :
{
  // Declaracion de variables 
  Token t;
  int total = 0, aux = 0;
}
{
  try
  {
    (
    aux = declaracion() t = < tPUNTYCOM >
    {
      total += aux;
    }
    ) *
    {
      return total;
    }
  }
  catch (ParseException e)
  {
    ErrorSintactico.deteccionErrorSintactico(e);
  }
}

// Regla para los tipos de variables OK
Simbolo.Tipo_variable tipos_variables() :
{
  // Declaracion de variable
  Token t;
}
{
  try
  {
    // Asignacion del token leido
    (
    t = < tENTERO >
  | t = < tCARACTER >
  | t = < tBOOLEANO >
    )
    {
      // Control del tipo token
      switch (t.kind)
      {
        case tENTERO : 
        // Es token tENTERO
        return Simbolo.Tipo_variable.ENTERO;
        break;
        case tCARACTER : 
        // Es token tCARACTER
        return Simbolo.Tipo_variable.CHAR;
        break;
        case tCONSTCAD : 
        // Es token tCARACTER
        return Simbolo.Tipo_variable.CADENA;
        break;
        case tBOOLEANO : 
        // Es token tBOOLEANO
        return Simbolo.Tipo_variable.BOOLEANO;
        break;
        default : 
        // No es niguno de los anteriores es DESCONOCIDO
        return Simbolo.Tipo_variable.DESCONOCIDO;
      }
    }
  }
  catch (ParseException e)
  {
    ErrorSintactico.deteccionErrorSintactico(e);
  }
}

// Regla de declaracion OK
int declaracion() :
{
  // Declaracion de variables
  Token t;
  Simbolo.Tipo_variable tp_Var;
  LinkedList < String > lista = new LinkedList < String > ();
  // Variable para guardar el identificador del simbolo a introducir
  String identificadorActual;
  Simbolo s;
}
{
  try
  {
    tp_Var = tipos_variables() 
    lista = identificadores()
    {
      // Tamaño de la lista de identificadores
      int dimension = lista.size();
      //Bucle de recorrido de insercion de variables
      for (int i = 0; i < dimension; i++)
      {
        // Obtencion del identificador actual
        identificadorActual = lista.get(i);
        // Simbolo ya existente en la tabla de simbolos
        // introducir el nuevo simbolo
        s = tabla.introducir_variable(identificadorActual, tp_Var, nivel, dir);
        // Incremento de la variable de direccion
        if (i <= dimension - 1)
        {
          // Preparar direccion del siguiente parametro si no es el ultimo
          incrementar_pila();
        }
        // Comprobar que la variable esta en la tabla de simbolos
        if (s == null)
        {
          ErrorSemantico.deteccionErrorSemantico("linea " + token.beginLine + 
          ", columna " + token.beginColumn + "  - Variable repetida " + identificadorActual);
        }
      }
      // Devuelvo el numero de identificadores leidos;
      return lista.size();
    }
  }
  catch (ParseException e)
  {
    ErrorSintactico.deteccionErrorSintactico(e);
  }
}

// Regla para los identificadores OK
LinkedList < String > identificadores() :
{
  // Declaracion de una lista auxiliar de identificadores
  Token t;
  // Lista de identificadores (parametros o variables)
  LinkedList < String > listaIdentificadores = new LinkedList < String > ();
}
{
  try
  {
    // Coger primer identificador
    t = < tIDENTIFICADOR >
    {
      // Añadir el identificador a lista
      listaIdentificadores.add(t.image);
    }
    (< tCOMA > 
    // Coger los siguientes identificadores
    t = < tIDENTIFICADOR >
    {
      // Añadir el identificador a lista
      listaIdentificadores.add(t.image);
    }
    ) *
    // Todos los identificadores
    {
      return listaIdentificadores;
    }
  }
  catch (ParseException e)
  {
    ErrorSintactico.deteccionErrorSintactico(e);
  }
}

// Regla para las clases de parametros OK
Simbolo.Clase_parametro clase_parametros() :
{
  // Declaracion de variables
  Token t;
}
{
  try
  {
    // Asignacion del token hallado
    (
    t = < tVAL >
  | t = < tREF >
    )
    {
      if (t.kind == tVAL)
      {
        // Es token tVAL
        return Simbolo.Clase_parametro.VAL;
      }
      else if (t.kind == tREF)
      {
        // Es tokeb tREF
        return Simbolo.Clase_parametro.REF;
      }
      else
      {
        // Clase de parametro erroneo
        ErrorSemantico.deteccionErrorSemantico("linea " + token.beginLine + 
        ", columna " + token.beginColumn + "  - La clase de parametro no es correcta");
      }
      // El tipo de clase ha sido procesada
    }
  }
  catch (ParseException e)
  {
    ErrorSintactico.deteccionErrorSintactico(e);
  }
}
